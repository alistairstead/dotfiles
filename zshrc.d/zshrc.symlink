#!/bin/sh

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$HOME/.dotfiles"

# your project folder that we can `t [tab]` to
export PROJECTS="$HOME/github"

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load antibody plugins
. ~/.zsh_plugins.sh

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  . "$file"
done

autoload -Uz compinit
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit -i
else
  compinit -C -i
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  . "$file"
done

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

unset config_files updated_at

[ -f ~/.fzf.zsh ] && . ~/.fzf.zsh

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
# shellcheck disable=SC1090
[ -f ~/.localrc ] && . ~/.localrc

_direnv_hook() {
  eval "$("/usr/local/bin/direnv" export zsh)";
}
typeset -ag precmd_functions;
if [[ -z ${precmd_functions[(r)_direnv_hook]} ]]; then
  precmd_functions+=_direnv_hook;
fi

# welcome!
hash ponysay &>2 && ponysay -o
