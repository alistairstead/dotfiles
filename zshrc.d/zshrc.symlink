#!/usr/bin/env zsh

# shortcut to this dotfiles path is $DOTFILES
autoload -U +X compinit && compinit
# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

source $DOTFILES/zshrc.d/fpath.zsh

# all of our zsh files
config_files=($DOTFILES/**/*.zsh)

# load everything but the env, path and completion files
for file in ${config_files:#*/(completion|path|env).zsh}
do
  source "$file"
done


# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source "$file"
done

unset config_files
